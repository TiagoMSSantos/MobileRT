///////////////////////////////////////////////////////////////////////////////
// Apply plugins
///////////////////////////////////////////////////////////////////////////////
plugins {
    // JaCoCo plugin provides code coverage metrics for Java code via
    // integration with JaCoCo.
    id 'jacoco'

    // Application plugin facilitates creating an executable JVM application.
    id 'com.android.application'

    // Pitest provides an ability to perform a mutation testing and calculate a
    // mutation coverage of a Gradle-based projects with PIT.
//    id 'info.solidsoft.pitest'
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Get system components versions from properties
///////////////////////////////////////////////////////////////////////////////
println("Reading system properties.")

final def systemNDKVersion = System.getProperty('ndkVersion', '21.3.6528147')
final def systemCMakeVersion = System.getProperty('cmakeVersion', '3.10.2')
final def systemTestType = System.getProperty('testType', 'debug')

println("NDK version: " + systemNDKVersion)
println("CMake version: " + systemCMakeVersion)
println("Test type: " + systemTestType)
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Add flags to Java compiler
///////////////////////////////////////////////////////////////////////////////
allprojects {
    tasks.withType(JavaCompile) {
        println("Adding additional flags to the compiler.")

        options.compilerArgs \
            << '-Xlint:all' \
            << '-AprintErrorStack' \
            << '-AprintAllQualifiers' \
            << '-verbose'
    }
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Android setup
///////////////////////////////////////////////////////////////////////////////
android {
    // Set NDK version
    setNdkVersion systemNDKVersion

    // Set build type for the tests
    setTestBuildType systemTestType

    // Setup required versions to compile
    setCompileSdkVersion 29
    setBuildToolsVersion '30.0.0'

    // Setup signing configurations
    signingConfigs {
        Puscas {
            setStoreFile file('MobileRT.jks')
            setStorePassword '123456'
            setKeyPassword '123456'
            setKeyAlias 'Puscas'
        }
    }

    // Setup default configurations
    defaultConfig {
        setMinSdkVersion 16
        setTargetSdkVersion 29
        setTestInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        setTestInstrumentationRunnerArguments clearPackageData: 'true'
        setApplicationId 'puscas.mobilertapp'
        setVersionName '1.0'
        setVersionCode 1
        setMultiDexEnabled true
        setTestHandleProfiling false
        setTestFunctionalTest false
        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            setModuleName 'MobileRT'
        }
        externalNativeBuild {
            cmake {
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
            }
        }
    }

    // Setup native configurations
    externalNativeBuild {
        cmake {
            setPath 'CMakeLists.txt'
            setVersion systemCMakeVersion
        }
    }

    // Set Java version
    compileOptions {
        setDefaultJavaVersion JavaVersion.VERSION_1_8
        setTargetCompatibility JavaVersion.VERSION_1_8
        setSourceCompatibility JavaVersion.VERSION_1_8
    }

    // Set build types
    buildTypes {
        debug {
            setDebuggable true
            setJniDebuggable true
            setTestCoverageEnabled true
            setVersionNameSuffix 'd'
            setRenderscriptDebuggable true
            setRenderscriptOptimLevel 0
            setMinifyEnabled false
            setSigningConfig signingConfigs.Puscas
            setShrinkResources false
            ndk {
            }
        }
        release {
            setDebuggable false
            setJniDebuggable false
            setTestCoverageEnabled false
            setVersionNameSuffix 'r'
            setRenderscriptDebuggable false
            setRenderscriptOptimLevel 3
            setMinifyEnabled false
            setSigningConfig signingConfigs.Puscas
            setShrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
            }
        }
    }

    // Setup product flavors
    productFlavors {
    }

    // Exclude some files from the package due to conflicts from different dependencies
    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/licenses/ASM'
    }

    // Configure test options
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        setAnimationsDisabled true
//        setExecution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    // Setup DEX options
    dexOptions {
        setJavaMaxHeapSize '1mb'
    }

    // Setup linter options
    lintOptions {
        setAbortOnError true
        setCheckAllWarnings true
        setExplainIssues true
        setNoLines true
        setShowAll true
        setWarningsAsErrors false
        setCheckDependencies true
        setAbsolutePaths false
        setCheckGeneratedSources true
        setCheckReleaseBuilds true
        setCheckTestSources true
        setHtmlReport true
        setXmlReport true
        setTextReport true
        setIgnoreTestSources false
        setIgnoreWarnings false
        setQuiet false
        enable 'WrongThreadInterprocedural', 'RtlHardcoded','RtlCompat', 'RtlEnabled'
        lintConfig file('lint.xml')
    }

    // Add resources directories to tests
    sourceSets {
        // Unit tests
        test {
            resources.srcDirs += ['src/test/resources']
        }
        // Instrumentation tests
        androidTest {
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
            resources.srcDirs += ['src/androidTest/resources']
        }
    }
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Add configurations
///////////////////////////////////////////////////////////////////////////////
configurations {
    debug
    release
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Add third party dependencies
///////////////////////////////////////////////////////////////////////////////
dependencies {
    // Dependencies for the Android application
    println("Adding dependencies for MobileRT.")

    // Android dependencies
    implementation 'androidx.appcompat:appcompat:1.2.0'
//    implementation 'androidx.multidex:multidex:2.0.1'

    // Java dependencies
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.7.2'
    implementation 'org.jetbrains:annotations:20.1.0'

    // More dependencies
    implementation 'org.apache.commons:commons-lang3:3.11'



    // Dependencies for the Android instrumentation tests
    println("Adding dependencies for the instrumentation tests.")

    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'

    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.3.0'

    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
//    androidTestUtil 'androidx.test:orchestrator:1.2.0'

//    androidTestImplementation 'org.mockito:mockito-android:3.4.6'
//    androidTestImplementation 'org.mockito:mockito-core:3.4.6'

    // Dependencies for the Android unit tests
    println("Adding dependencies for the unit tests.")

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.assertj:assertj-core:3.17.2'
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'



    // Dependencies that give errors when added
//    implementation 'org.apache.commons:commons-text:1.8'
//    androidTestImplementation 'org.assertj:assertj-core:3.16.1'
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
