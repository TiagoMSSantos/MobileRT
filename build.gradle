///////////////////////////////////////////////////////////////////////////////
// Setup necessary imports
///////////////////////////////////////////////////////////////////////////////
import groovy.json.JsonSlurper
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Setup Gradle
///////////////////////////////////////////////////////////////////////////////
buildscript {

    // Setup necessary repositories to get third party dependencies
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()

        maven { url 'https://maven.google.com' }
        maven { url 'https://mvnrepository.com' }
        maven { url 'https://dl.google.com/dl/android/maven2/' }
        maven { url 'https://plugins.gradle.org/m2/'}
        maven { url 'https://jitpack.io' }
    }

    // This block encapsulates custom properties and makes them available to all
    // modules in the project.
    ext {
        systemTestType = System.getProperty('testType', 'debug')
        final def customAbiFilters = System.getProperty('abiFilters', '["armeabi-v7a", "x86", "arm64-v8a", "x86_64"]')
        systemAbiFilters = new JsonSlurper().parseText(customAbiFilters) as String[]

        // The default minimum Android API version possible is 14 due to compatibility with Appcompat dependency.
        androidApiVersion = System.getProperty('androidApiVersion', '14')
        final def androidApiVersionArray = androidApiVersion.split("\\.").collect()
        androidApiVersionValue = androidApiVersionArray[0] as Float

        if (androidApiVersionValue >= 21) {
            androidX_appcompat_version = '1.7.0'
            ndk_version = '26.3.11579264'
        }
        if (androidApiVersionValue >= 19) {
            common_libraries_android_framework_version = '1.13.1'
            androidX_test_junit_version = '1.1.5'
            androidX_test_runner_version = '1.5.2'
            androidX_test_rules_version = '1.5.0'
            androidX_test_espresso_version = '3.5.1'
            androidX_test_monitor_version = '1.6.1'
            androidX_tracing_version = '1.0.0'
            androidX_activity_version = '1.7.2'
            androidX_startup_version = '1.1.1'
            androidX_emoji_version = '1.2.0'
            if (project.properties["ndk_version"] == null) {
                ndk_version = '25.2.9519653'
            }
        }
        if (androidApiVersionValue >= 16) {
            gradle_version = '8.5.0'
        } else {
            gradle_version = '8.2.2'
        }

        if (project.properties["androidX_test_monitor_version"] == null) {
            androidX_test_monitor_version = '1.6.1'
        }
        if (project.properties["androidX_test_espresso_version"] == null) {
            androidX_test_espresso_version = '3.5.1'
        }
        if (project.properties["androidX_test_junit_version"] == null) {
            androidX_test_junit_version = '1.1.5'
        }
        if (project.properties["androidX_test_runner_version"] == null) {
            androidX_test_runner_version = '1.5.2'
        }
        if (project.properties["androidX_test_rules_version"] == null) {
            androidX_test_rules_version = '1.5.0'
        }
        if (project.properties["androidX_appcompat_version"] == null) {
            androidX_appcompat_version = '1.6.1'
        }
        if (project.properties["common_libraries_android_framework_version"] == null) {
            common_libraries_android_framework_version = '1.12.0'
        }
        if (project.properties["ndk_version"] == null) {
            ndk_version = '23.2.8568313'
        }
        if (project.properties["androidX_tracing_version"] == null) {
            androidX_tracing_version = '1.0.0'
        }
        if (project.properties["androidX_activity_version"] == null) {
            androidX_activity_version = '1.7.2'
        }
        if (project.properties["androidX_startup_version"] == null) {
            androidX_startup_version = '1.1.1'
        }
        if (project.properties["androidX_emoji_version"] == null) {
            androidX_emoji_version = '1.2.0'
        }

        final def gradleVersionArray = gradle_version.split("\\.").collect()
        gradleVersionValue = gradleVersionArray[0] + '.' + gradleVersionArray[1] as Float

        kotlin_stdlib_jdk8_version = '2.0.0'
        androidX_annotation_version = '1.8.0'

        // Test dependencies
        jacoco_version = '0.8.12'

        // Unit tests dependencies
        powermock_version = '2.0.9'
    }

    dependencies {
        final def currentOS = System.getProperty("os.name")
        println("OS: " + currentOS)
        // Compatibility matrix: https://developer.android.com/build/releases/gradle-plugin#android_gradle_plugin_and_android_studio_compatibility
        classpath "com.android.tools.build:gradle:${gradle_version}"
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:5.0.0.4638'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_stdlib_jdk8_version}"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.51.0'

        /* To confirm JaCoCo version run: $ sh gradlew buildEnvironment */
        /* Resolves issue of incorrect version use in one of jacoco/android plugin inner tasks */
        classpath "org.jacoco:org.jacoco.core:${jacoco_version}"
        classpath "org.jacoco:org.jacoco.report:${jacoco_version}"
        classpath "org.jacoco:org.jacoco.agent:${jacoco_version}"

        // classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.15.0'
        // classpath 'org.pitest:pitest-command-line:1.15.3'
    }
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Setup necessary repositories to get third party dependencies
///////////////////////////////////////////////////////////////////////////////
allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()

        maven { url 'https://maven.google.com' }
        maven { url 'https://mvnrepository.com' }
        maven { url 'https://dl.google.com/dl/android/maven2/' }
        maven { url 'https://plugins.gradle.org/m2/'}
        maven { url 'https://jitpack.io' }
    }
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Allow to use different versions of Gradle depending on the Android API version
///////////////////////////////////////////////////////////////////////////////
tasks.named('wrapper', Wrapper) {it ->
    println('Setting Gradle wrapper version')
    it.distributionType = Wrapper.DistributionType.BIN
    if (androidApiVersionValue >= 16) {
        it.gradleVersion = '8.8'
    } else {
        it.gradleVersion = '8.7'
    }
    it.distributionUrl = 'https://services.gradle.org/distributions/gradle-' + it.gradleVersion + '-bin.zip'
    println('Gradle wrapper version: ' + it.gradleVersion)
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////

repositories {
    google()
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()

    maven { url 'https://maven.google.com' }
    maven { url 'https://mvnrepository.com' }
    maven { url 'https://dl.google.com/dl/android/maven2/' }
    maven { url 'https://plugins.gradle.org/m2/'}
    maven { url 'https://jitpack.io' }
}

/* Force Jacoco Agent version upgrade */
subprojects {
    configurations.configureEach {
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion "${jacoco_version}"
                }
                // Necessary because 'androidx.core:core-ktx' also brings the 'org.jetbrains.kotlin:kotlin-stdlib'.
                if ('org.jetbrains.kotlin' == details.requested.group) {
                    details.useVersion "${kotlin_stdlib_jdk8_version}"
                }
                if ('androidx.tracing:tracing' == details.requested.group) {
                    details.useVersion "${androidX_tracing_version}"
                }
                if ('androidx.activity:activity' == details.requested.group) {
                    details.useVersion "${androidX_activity_version}"
                }
                if ('androidx.startup:startup-runtime' == details.requested.group) {
                    details.useVersion "${androidX_startup_version}"
                }
            }
        }
    }
}
