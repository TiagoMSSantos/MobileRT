///////////////////////////////////////////////////////////////////////////////
// Setup necessary imports
///////////////////////////////////////////////////////////////////////////////
import groovy.json.JsonSlurper
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Setup Gradle
///////////////////////////////////////////////////////////////////////////////
buildscript {

    // Setup necessary repositories to get third party dependencies
    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
        mavenLocal()
        maven { url = 'https://mvnrepository.com' }
        maven { url = 'https://repo1.maven.org/maven2/' }
        maven { url = 'https://maven.google.com' }
        maven { url = 'https://plugins.gradle.org/m2/'}
        maven { url = 'https://jitpack.io' }
    }

    // This block encapsulates custom properties and makes them available to all
    // modules in the project.
    ext {
        systemTestType = System.getProperty('testType', 'debug')
        final def customAbiFilters = System.getProperty('abiFilters', '["x86","x86_64"]')
        systemAbiFilters = new JsonSlurper().parseText(customAbiFilters) as String[]

        // The default minimum Android API version possible is 14 due to compatibility with Appcompat dependency.
        androidApiVersion = System.getProperty('androidApiVersion', '14')
        final def androidApiVersionArray = androidApiVersion.split("\\.")
        if (androidApiVersionArray.size() != 1) {
            throw new GradleException("Android API version has an unexpected minor version: " + androidApiVersion);
        }
        final def androidApiVersionValueStr = androidApiVersionArray[0]
        androidApiVersionValue = androidApiVersionValueStr.toInteger()
        println('Android version 1: ' + androidApiVersion)
        println('Android version 2: ' + androidApiVersionValueStr)
        println('Android version 3: ' + androidApiVersionValue)

        if (androidApiVersionValue >= 21) {
            androidX_appcompat_version = '1.7.1'
            ndk_version = '29.0.14206865'
            androidX_core_version = '1.17.0'
            androidX_test_espresso_version = '3.7.0'
            androidX_test_junit_version = '1.3.0'
            androidX_test_core_version = '1.7.0'
        }
        if (androidApiVersionValue >= 19) {
            if (project.properties["androidX_test_junit_version"] == null) {
                androidX_test_junit_version = '1.2.1'
            }
            if (project.properties["androidX_test_espresso_version"] == null) {
                androidX_test_espresso_version = '3.6.1'
            }
            if (project.properties["ndk_version"] == null) {
                ndk_version = '25.2.9519653'
            }
            if (project.properties["androidX_core_version"] == null) {
                androidX_core_version = '1.13.1'
            }
            if (project.properties["androidX_test_core_version"] == null) {
                androidX_test_core_version = '1.6.1'
            }
        }
        if (androidApiVersionValue >= 16) {
            gradle_version = '9.0.0'
            gradle_versions_plugin_version = '0.52.0'
            kotlin_gradle_plugin_version = '2.3.0'
        }

        // Default dependencies versions which are used on all Android APIs supported >= 14
        if (project.properties["androidX_appcompat_version"] == null) {
            androidX_appcompat_version = '1.6.1'
        }
        if (project.properties["ndk_version"] == null) {
            ndk_version = '23.2.8568313'
        }
        if (project.properties["androidX_core_version"] == null) {
            androidX_core_version = '1.12.0'
        }
        if (project.properties["gradle_version"] == null) {
            gradle_version = '8.2.2'
        }
        if (project.properties["gradle_versions_plugin_version"] == null) {
            gradle_versions_plugin_version = '0.51.0'
        }
        if (project.properties["kotlin_gradle_plugin_version"] == null) {
            kotlin_gradle_plugin_version = '2.0.21'
        }

        final def gradleVersionArray = gradle_version.split("\\.")
        gradleVersionMajor = gradleVersionArray[0].toInteger()
        gradleVersionMinor = gradleVersionArray[1].toInteger()

        // Test dependencies
        jacoco_version = '0.8.14'
        if (project.properties["androidX_test_espresso_version"] == null) {
            androidX_test_espresso_version = '3.5.1'
        }
        if (project.properties["androidX_test_junit_version"] == null) {
            androidX_test_junit_version = '1.1.5'
        }
        if (project.properties["bouncycastle_version"] == null) {
            bouncycastle_version = '1.83'
        }
        if (project.properties["androidX_test_core_version"] == null) {
            androidX_test_core_version = '1.5.0'
        }

        // Unit tests dependencies
        powermock_version = '2.0.9'
    }

    dependencies {
        final def currentOS = System.getProperty("os.name")
        println("OS: " + currentOS)
        // Compatibility matrix: https://developer.android.com/build/releases/gradle-plugin#android_gradle_plugin_and_android_studio_compatibility
        classpath "com.android.tools.build:gradle:${gradle_version}"
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:7.2.2.6593'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_gradle_plugin_version}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${gradle_versions_plugin_version}"

        /* To confirm JaCoCo version run: $ sh gradlew buildEnvironment */
        /* Resolves issue of incorrect version use in one of jacoco/android plugin inner tasks */
        classpath "org.jacoco:org.jacoco.core:${jacoco_version}"
        classpath "org.jacoco:org.jacoco.report:${jacoco_version}"
        classpath "org.jacoco:org.jacoco.agent:${jacoco_version}"

        // Required for sonarqube-gradle-plugin:v6
        classpath "org.bouncycastle:bcutil-jdk18on:${bouncycastle_version}"
        classpath "org.bouncycastle:bcpkix-jdk18on:${bouncycastle_version}"
    }
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Allow to use different versions of Gradle depending on the Android API version
///////////////////////////////////////////////////////////////////////////////
tasks.named('wrapper', Wrapper) {it ->
    println('Setting Gradle wrapper version')
    it.distributionType = Wrapper.DistributionType.BIN
    if (androidApiVersionValue >= 16) {
        it.gradleVersion = '9.3.1'
    } else {
        it.gradleVersion = '8.7'
    }
    var urlPrefix = 'https://services.gradle.org/distributions/'
    it.distributionUrl = urlPrefix + 'gradle-' + it.gradleVersion + '-' + it.distributionType.name().toLowerCase() + '.zip'
    println('Gradle wrapper version: ' + it.gradleVersion)
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Enforce some dependencies versions because they are used by multiple dependencies
///////////////////////////////////////////////////////////////////////////////
subprojects {
    configurations.configureEach {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'org.jacoco') {
                    details.useVersion "${jacoco_version}"
                }
                if (details.requested.group == 'com.android.application') {
                    details.useVersion "${gradle_version}"
                }
            }
        }
    }
}
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
